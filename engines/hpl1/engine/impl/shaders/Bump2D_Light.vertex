// Copyright 2006-2010 (C) - Frictional Games
//
// This file is part of HPL1 Engine
//
// For conditions of distribution and use, see copyright notice in LICENSE-shaders
//

///////////////////////////////////////////////////////
/// BUMPMAPPING 2D VERTEX PROGRAM ////////////
///////////////////////////////////////////////////////

vec4 position = gl_Vertex;
vec3 normal = gl_Normal;
vec3 uv = gl_MultiTexCoord0.xyz;


out vec4 vColor;
out vec3 vUv;
out vec3 vLighDir;
out vec2 vScreenPos;

uniform mat4 worldViewProj;
uniform vec3 LightPos;
uniform float LightRadius;
uniform vec4 LightColor;

void main()
{
	vec3 L;

	//Get the direction of the light and normalize it
	vLighDir.xy = position.xy - LightPos.xy;
	vScreenPos = (vLighDir.xy + (LightRadius) )/ (LightRadius*2.0);
	//rotate the light to texture (tangent) space. Normal x=cos(angle) y = sin(angle)
	vec2 TempLight = vLighDir.xy;
	vLighDir.x = normal.x*TempLight.x + normal.y*TempLight.y;
	vLighDir.y = normal.x*TempLight.y - normal.y*TempLight.x;
	vLighDir.x *= normal.z/abs(normal.z);
	vLighDir.y *= abs(normal.z)-2.0;

	vLighDir.z = -LightPos.z;
	vLighDir = normalize(vLighDir);

	gl_Position = worldViewProj * position;

	vColor = LightColor;
	vUv = uv;
}
